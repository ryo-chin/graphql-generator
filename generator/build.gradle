plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id "com.gradle.plugin-publish" version "0.10.1"
    id "java-gradle-plugin"
}

version = '0.0.2'
group = "com.github.ryo-chin"
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

// ---- Gradle Plugin Settings ----
// ref. https://plugins.gradle.org/docs/publish-plugin
pluginBundle {
    website = 'https://github.com/ryo-chin/graphql-generator'
    vcsUrl = 'https://github.com/ryo-chin/graphql-generator'
    description = 'GraphQL Generator'
    tags = ['GraphQL', 'AutoGen']

    plugins {
        graphqlGeneratorPlugin {
            id = 'com.github.ryo-chin.graphql-generator'
            displayName = 'GraphQL Auto Generate plugin'
        }
    }
}
// --------------------------------

dependencies {
    implementation gradleApi()
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'com.graphql-java:graphql-java-tools:2.1.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
}

jar {
    baseName = 'graphql-generator'
    manifest {
        attributes "Main-Class": "GeneratorKt"
    }
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA' // gradle refresh fails without this line when "apply plugin"
}
